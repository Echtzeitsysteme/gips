grammar org.emoflon.roam.roamslang.RoamSLang with org.emoflon.ibex.gt.editor.GT

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.emoflon.org/ibex/gt/editor/GT" as GT

generate roamSLang "http://www.emoflon.org/roam/roamslang/RoamSLang"

@Override 
EditorGTFile: {EditorGTFile} 
	(imports+=EditorImport)*
	config = RoamConfig
  	(patterns+=EditorPattern |
  	conditions+=EditorCondition |
  	mappings += RoamMapping |
  	constraints += RoamConstraint |
  	objectives += RoamObjective)*
  	(globalObjective = RoamGlobalObjective)?	
;

RoamConfig : {RoamConfig}
	'config' '{'
	'solver' ':=' solver=SolverType '[' 'home' ':=' home=RoamStringLiteral ',' 'license' ':=' license=RoamStringLiteral ']' ';'
	('launchConfig' ':=' enableLaunchConfig = RoamBoolean '[' 'main' ':=' mainLoc=RoamStringLiteral ']' ';')?
	('timeLimit' ':=' enableLimit = RoamBoolean '[' 'value' ':=' timeLimit=RoamDouble ']' ';')?
	('randomSeed' ':=' enableSeed = RoamBoolean '[' 'value' ':=' rndSeed=RoamInteger ']' ';')?
	('presolve' ':=' enablePresolve = RoamBoolean ';')?
	('debugOutput' ':=' enableDebugOutput = RoamBoolean ';')?
	'}'
;

enum SolverType:
	GUROBI='GUROBI'
;

RoamMapping :
	'mapping' name=ID 'with' rule=[GT::EditorPattern|ID] ';'
;

RoamConstraint : 
	'constraint' '->' context=(RoamMappingContext | RoamTypeContext | RoamMatchContext) '{'
		expr = RoamBool
	'}'
;

RoamObjective : 
	'objective' name=ID '->' context=(RoamMappingContext | RoamTypeContext | RoamMatchContext) '{'
		expr = RoamArithmeticExpr
	'}'
;

RoamMappingContext:
	'mapping::' mapping = [RoamMapping|ID]
;

RoamTypeContext:
	'class::' type = [ecore::EClassifier]
;

RoamMatchContext:
	'match::' pattern = [GT::EditorPattern|ID]
;

RoamObjectiveExpression:
	objective=[RoamObjective|ID]
;

RoamGlobalObjective :
	'global' 'objective' ':' objectiveGoal = RoamObjectiveGoal ('{'
		expr = RoamArithmeticExpr
	'}')?
;

enum RoamObjectiveGoal:
	MIN = 'min' | MAX = 'max'
;

RoamBool:
	expr=RoamBoolExpr
;

RoamBoolExpr:
	RoamBinaryBoolExpr
;

RoamBinaryBoolExpr returns RoamBoolExpr:
	RoamUnaryBoolExpr({RoamBinaryBoolExpr.left = current} operator=RoamBoolBinaryOperator right=RoamUnaryBoolExpr)*
;

RoamUnaryBoolExpr returns RoamBoolExpr: {RoamUnaryBoolExpr}
	(operator=RoamBoolUnaryOperator '(' operand=RoamBoolExpr ')' ) | RoamBooleanLiteral | RoamRelExpr
;

RoamBooleanLiteral:
	literal = RoamBoolean
;

enum RoamBoolBinaryOperator:
	AND='&' 	|
	OR='|'	
;

enum RoamBoolUnaryOperator:
	NOT = '!'
;

RoamRelExpr:
	left=RoamArithmeticExpr (operator=RoamRelOperator right=RoamArithmeticExpr)?
;

enum RoamRelOperator:
  GREATER='>' |
  GREATER_OR_EQUAL='>=' |
  EQUAL='==' |
  UNEQUAL='!=' |
  SMALLER_OR_EQUAL='<=' |
  SMALLER='<'
;

RoamAttributeExpr:
	RoamMappingAttributeExpr |  RoamContextExpr | RoamLambdaAttributeExpression
;

RoamMappingAttributeExpr returns RoamAttributeExpr: {RoamMappingAttributeExpr}
	'mappings.' mapping=[RoamMapping|ID] '->' expr = RoamStreamExpr
;

//TODO: Allow optional nested lambda expressions
//TODO: Allow omission of "expr" rule call to enable usage of objects in lambda expression, e.g., for identity / type checks.
RoamLambdaAttributeExpression returns RoamAttributeExpr: {RoamLambdaAttributeExpression}
	var=[RoamLambdaExpression|ID] '.' (expr = RoamNodeAttributeExpr | expr = RoamContextOperationExpression | expr = RoamFeatureExpr) // ('->' stream = RoamStreamExpr)?
;

RoamContextExpr returns RoamAttributeExpr: {RoamContextExpr}
	'self' ('.' typeCast=RoamTypeCast)? ('.' (expr = RoamNodeAttributeExpr | expr = RoamContextOperationExpression | expr = RoamFeatureExpr) ('->' stream = RoamStreamExpr)?)?
;

RoamContextOperationExpression:
	operation=RoamContextOperation
;

enum RoamContextOperation:
	VALUE = 'value()' |
 	MAPPED = 'isMapped()'
;

RoamTypeCast:
	'toType(' type=[ecore::EClass] ')'
;

RoamNodeAttributeExpr: {RoamNodeAttributeExpr}
	'nodes().' node=[GT::EditorNode|ID] ('.' typeCast=RoamTypeCast)? ('.' expr = RoamFeatureExpr)?
;

RoamFeatureExpr:
	RoamFeatureNavigation
;

RoamFeatureNavigation returns RoamFeatureExpr:
	RoamFeatureLit ({RoamFeatureNavigation.left=current} '.' right=RoamFeatureNavigation)?
;

RoamFeatureLit returns RoamFeatureExpr: {RoamFeatureLit}
	feature = [ecore::EStructuralFeature] ('.' typeCast=RoamTypeCast)?
;

RoamArithmeticExpr:
	RoamSumArithmeticExpr
;

RoamSumArithmeticExpr returns RoamArithmeticExpr:
	RoamProductArithmeticExpr({RoamSumArithmeticExpr.left = current} operator=RoamSumOperator right=RoamProductArithmeticExpr)*
;

RoamProductArithmeticExpr returns RoamArithmeticExpr:
	RoamExpArithmeticExpr({RoamProductArithmeticExpr.left = current} operator=RoamProductOperator right=RoamExpArithmeticExpr)*
;

RoamExpArithmeticExpr returns RoamArithmeticExpr:
	RoamUnaryArithmeticExpr({RoamExpArithmeticExpr.left = current} operator=RoamExpOperator right=RoamUnaryArithmeticExpr)*
;

RoamUnaryArithmeticExpr returns RoamArithmeticExpr: {RoamUnaryArithmeticExpr}
	operator=RoamArithmeticUnaryOperator  '(' operand=RoamBracketExpr ')' | RoamBracketExpr
;

RoamBracketExpr returns RoamArithmeticExpr: {RoamBracketExpr}
	'(' operand=RoamArithmeticExpr ')' | RoamExpressionOperand
;

RoamExpressionOperand:
	RoamAttributeExpr | RoamObjectiveExpression | RoamArithmeticLiteral
;

RoamArithmeticLiteral:
	value = (RoamDoubleLiteral | RoamIntegerLiteral)
;

enum RoamProductOperator:
	MULT='*' |
 	DIV='/'  
;

enum RoamExpOperator:
 	POW='^'	
;

enum RoamSumOperator:
	PLUS='+' 	|
	MINUS='-' 	
;

enum RoamArithmeticUnaryOperator:
	NEG='-'	|
	ABS='abs'	|
	SQRT='sqrt' |
	sin='sin'	|
	cos='cos'	
;

RoamStreamExpr:
	RoamStreamNavigation
;

RoamStreamNavigation returns RoamStreamExpr:
	RoamStreamLit({RoamStreamNavigation.left=current}'->' right=RoamStreamNavigation)?
;

RoamStreamLit returns RoamStreamExpr:
	{RoamSelect} 'typesOf' '(' type=[ecore::EClassifier] ')' |
	{RoamStreamSet} operator=RoamStreamSetOperator lambda = RoamLambdaExpression |
	{RoamStreamArithmetic} operator=RoamStreamArithmeticOperator lambda = RoamLambdaExpression |
	{RoamStreamBoolExpr} operator=RoamStreamNoArgOperator
;

RoamLambdaExpression:
	'(' name=ID '|' expr=RoamBoolExpr ')'
;

enum RoamStreamSetOperator:
	FILTER='filter'
;

enum RoamStreamArithmeticOperator:
	SUM = 'sum'
;

enum RoamStreamNoArgOperator:
	EXISTS = 'exists()' |
	NOTEXISTS = 'notExists()' |
	count = 'count()'
;

RoamDoubleLiteral:
	RoamDouble
;

RoamIntegerLiteral:
	RoamInteger
;

RoamDouble returns ecore::EDouble:
	('-')?INT '.' INT
;

RoamInteger returns ecore::EInt:
	(('-')? INT)
;

@Override
terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9')*
;

RoamBoolean returns ecore::EBoolean:
	TRUE | FALSE
;

terminal TRUE returns ecore::EBoolean:
	'true'
;

terminal FALSE returns ecore::EBoolean:
	'false'
;

RoamStringLiteral:
	STRING
;

@Override
terminal STRING returns ecore::EString:
	'"' ( '""' | !('"') )* '"'
;