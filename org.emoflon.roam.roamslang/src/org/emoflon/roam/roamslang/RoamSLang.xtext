grammar org.emoflon.roam.roamslang.RoamSLang with org.emoflon.ibex.gt.editor.GT

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.emoflon.org/ibex/gt/editor/GT" as GT

generate roamSLang "http://www.emoflon.org/roam/roamslang/RoamSLang"

@Override 
EditorGTFile: {EditorGTFile} 
	(imports+=EditorImport)*
  	(patterns+=EditorPattern |
  	conditions+=EditorCondition |
  	mappings += RoamMapping |
  	constraints += RoamConstraint |
  	objectives += RoamObjective)*
  	(globalObjective = RoamGlobalObjective)?	
;

RoamMapping :
	'mapping' name=ID 'with' rule=[GT::EditorPattern|ID] ';'
;

RoamConstraint : 
	'constraint' '->' context=(RoamMappingContext | RoamTypeContext) '{'
		expr = RoamBool
	'}'
;

RoamObjective : 
	'objective' name=ID '->' context=(RoamMappingContext | RoamTypeContext) '{'
		expr = RoamArithmeticExpr
	'}'
;

RoamMappingContext:
	'mapping::' mapping = [RoamMapping|ID]
;

RoamTypeContext:
	'class::' type = [ecore::EClassifier]
;

RoamObjectiveExpression:
	objective=[RoamObjective|ID]
;

RoamGlobalObjective :
	'global' 'objective' ':' objectiveGoal = RoamObjectiveGoal ('{'
		expr = RoamArithmeticExpr
	'}')?
;

enum RoamObjectiveGoal:
	MIN = 'min' | MAX = 'max'
;

RoamBool:
	expr=RoamBoolExpr
;

RoamBoolExpr:
	RoamBinaryBoolExpr
;

RoamBinaryBoolExpr returns RoamBoolExpr:
	RoamUnaryBoolExpr({RoamBinaryBoolExpr.left = current} operator=RoamBoolBinaryOperator right=RoamUnaryBoolExpr)*
;

RoamUnaryBoolExpr returns RoamBoolExpr: {RoamUnaryBoolExpr}
	(operator=RoamBoolUnaryOperator '(' operand=RoamBoolExpr ')' ) | RoamBooleanLiteral | RoamRelExpr
;

//RoamBoolLit returns RoamBoolExpr:
//	RoamRelExpr  |  RoamAttributeExpr |  RoamBooleanLiteral
//;

RoamBooleanLiteral:
	value = RoamBOOLEAN
;

enum RoamBoolBinaryOperator:
	AND='&' 	|
	OR='|'	
;

enum RoamBoolUnaryOperator:
	NOT = '!'
;

RoamRelExpr:
	left=RoamArithmeticExpr (operator=RoamRelOperator right=RoamArithmeticExpr)?
;

enum RoamRelOperator:
  GREATER='>' |
  GREATER_OR_EQUAL='>=' |
  EQUAL='==' |
  UNEQUAL='!=' |
  SMALLER_OR_EQUAL='<=' |
  SMALLER='<'
;

RoamAttributeExpr:
	RoamMappingAttributeExpr |  RoamContextExpr | RoamLambdaAttributeExpression
;

RoamMappingAttributeExpr returns RoamAttributeExpr: {RoamMappingAttributeExpr}
	'mappings.' mapping=[RoamMapping|ID] '->' expr = RoamStreamExpr
;

RoamLambdaAttributeExpression returns RoamAttributeExpr: {RoamLambdaAttributeExpression}
	var=[RoamLambdaExpression|ID] '.' (expr = RoamNodeAttributeExpr | expr = RoamContextOperationExpression | expr = RoamFeatureExpr)
;

RoamContextExpr returns RoamAttributeExpr: {RoamContextExpr}
	'self' ('.' typeCast=RoamTypeCast)? ('.' (expr = RoamNodeAttributeExpr | expr = RoamContextOperationExpression | expr = RoamFeatureExpr) ('->' stream = RoamStreamExpr)?)?
;

RoamContextOperationExpression:
	operation=RoamContextOperation
;

enum RoamContextOperation:
	VALUE = 'value()' |
 	MAPPED = 'isMapped()'
;

RoamTypeCast:
	'toType(' type=[ecore::EClass] ')'
;

RoamNodeAttributeExpr: {RoamNodeAttributeExpr}
	'nodes().' node=[GT::EditorNode|ID] ('.' typeCast=RoamTypeCast)? ('.' expr = RoamFeatureExpr)?
;

RoamFeatureExpr:
	RoamFeatureNavigation
;

RoamFeatureNavigation returns RoamFeatureExpr:
	RoamFeatureLit ({RoamFeatureNavigation.left=current} '.' right=RoamFeatureNavigation)?
;

RoamFeatureLit returns RoamFeatureExpr: {RoamFeatureLit}
	feature = [ecore::EStructuralFeature] ('.' typeCast=RoamTypeCast)?
;

RoamArithmeticExpr:
	RoamBinaryArithmeticExpr
;

RoamBinaryArithmeticExpr returns RoamArithmeticExpr:
	RoamUnaryArithmeticExpr({RoamBinaryArithmeticExpr.left = current} operator=RoamArithmeticBinaryOperator right=RoamUnaryArithmeticExpr)*
;

RoamUnaryArithmeticExpr returns RoamArithmeticExpr: {RoamUnaryArithmeticExpr}
	(negative?='-') (operator=RoamArithmeticUnaryOperator'(' | '(') operand=RoamArithmeticExpr')' | RoamExpressionOperand
;

RoamExpressionOperand:
	RoamAttributeExpr | RoamObjectiveExpression | RoamArithmeticLiteral
;

RoamArithmeticLiteral:
	value = (RoamDoubleLiteral | RoamIntegerLiteral)
;

enum RoamArithmeticBinaryOperator:
	PLUS='+' 	|
	MINUS='-' 	|
	MULT='*'|
 	DIV='/' 	|
 	POW='^'	
;

enum RoamArithmeticUnaryOperator:
	NONE='$NO_OP'     |
	ABS='abs'	|
	SQRT='sqrt' |
	sin='sin'	|
	cos='cos'	
;

RoamStreamExpr:
	RoamStreamNavigation
;

RoamStreamNavigation returns RoamStreamExpr:
	RoamStreamLit({RoamStreamNavigation.left=current}'->' right=RoamStreamNavigation)?
;

RoamStreamLit returns RoamStreamExpr:
	{RoamSelect} 'typesOf' '(' type=[ecore::EClassifier] ')' |
	{RoamStreamSet} operator=RoamStreamSetOperator lambda = RoamLambdaExpression |
	{RoamStreamArithmetic} operator=RoamStreamArithmeticOperator lambda = RoamLambdaExpression |
	{RoamStreamBoolExpr} operator=RoamStreamNoArgOperator
;

RoamLambdaExpression:
	'(' name=ID '|' expr=RoamBoolExpr ')'
;

enum RoamStreamSetOperator:
	FILTER='filter'
;

enum RoamStreamArithmeticOperator:
	SUM = 'sum'
;

enum RoamStreamNoArgOperator:
	EXISTS = 'exists()' |
	NOTEXISTS = 'notExists()' |
	count = 'count()'
;

RoamDoubleLiteral:
	RoamDouble
;

RoamIntegerLiteral:
	RoamInteger
;

//RoamStringLiteral:
//	value = STRING
//;

//RoamEnumLiteral:
//	'enum::' value = [ecore::EEnumLiteral]
//;

RoamDouble returns ecore::EDouble:
	('-')?INT '.' INT
;

RoamInteger returns ecore::EInt:
	(('-')? INT)
;

@Override
terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9')*
;

@Override
terminal STRING returns ecore::EString:
	'"' ( '""' | !('"') )* '"'
;

RoamBOOLEAN returns ecore::EBoolean:
	TRUE | FALSE
;

terminal TRUE returns ecore::EBoolean:
	'true'
;

terminal FALSE returns ecore::EBoolean:
	'false'
;