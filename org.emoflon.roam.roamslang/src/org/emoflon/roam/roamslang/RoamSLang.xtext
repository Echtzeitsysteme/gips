grammar org.emoflon.roam.roamslang.RoamSLang with org.emoflon.ibex.gt.editor.GT

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.emoflon.org/ibex/gt/editor/GT" as GT

generate roamSLang "http://www.emoflon.org/roam/roamslang/RoamSLang"

@Override 
EditorGTFile: {EditorGTFile} 
	(imports+=EditorImport)*
  	(patterns+=EditorPattern |
  	conditions+=EditorCondition |
  	mappings += RoamMapping |
  	constraints += RoamConstraint |
  	objectives += RoamObjective)*
  	(globalObjective = RoamGlobalObjective)?	
;


RoamMapping :
	'mapping' name=ID 'with' rule=[GT::EditorPattern|ID] ';'
;

RoamConstraint : 
	'constraint' '->' RoamConstraintContext '{'
		expr = RoamBoolExpr
	'}'
;

RoamConstraintContext:
	context = [RoamMapping|ID] | 'class::' context = [ecore::EClassifier]
;

RoamObjective : 
	'objective' '->' context=[RoamMapping|ID] '{'
		expr = RoamArithmeticExpr
	'}'
;

RoamGlobalObjective :
	'global' 'objective' ':' objectiveGoal = RoamObjectiveGoal '{'
		expr = RoamArithmeticExpr
	'}'
;

enum RoamObjectiveGoal:
	MIN = 'min' | MAX = 'max'
;

RoamAttributeExpr:
	RoamContextExpr | RoamMappingAttributeExpr | RoamLambdaAttributeExpression
;

RoamContextExpr:
	'self' '.' RoamContextAttributeExpr | RoamContextVariableExpr
;

RoamContextAttributeExpr:
	expr = RoamNodeAttributeExpr
;

RoamContextVariableExpr:
	operation = RoamContextOperation
;

enum RoamContextOperation:
	VALUE = 'value()' |
 	MAPPED = 'isMapped()'
;

RoamMappingAttributeExpr:
	'mappings' '.' mapping=[RoamMapping|ID] '.' expr = RoamNodeAttributeExpr
;


RoamNodeAttributeExpr:
	node=[GT::EditorNode|ID] '.' expr = RoamFeatureExpr
;

RoamFeatureExpr:
	RoamFeatureLit({RoamFeatureNavigation.left=current} ('.' right=RoamFeatureLit))*
;

RoamFeatureLit returns RoamFeatureExpr: {RoamFeatureLit}
	feature=[ecore::EStructuralFeature|ID]
;

RoamLambdaAttributeExpression:
	var=[RoamLambdaExpression|ID] '.' expr = RoamFeatureExpr
;

RoamStream:
	set = (RoamAttributeExpr ) '.' expr = RoamStreamExpr
;

RoamStreamExpr:
	RoamStreamLit({RoamStreamNavigation.left=current} ('.' right=RoamStreamLit))*
;

RoamStreamLit returns RoamStreamExpr:
	{RoamSelect} 'typesOf' '(' type=[ecore::EClassifier] ')' |
	{RoamStreamSet} operator=RoamStreamSetOperator lambda = RoamLambdaExpression |
	{RoamStreamArithmetic} operator=RoamStreamArithmeticOperator lambda = RoamLambdaExpression |
	{RoamStreamBoolExpr} operator=RoamStreamBooleanOperator '()'
;

RoamLambdaExpression:
	'(' name=ID '|' expr=RoamBoolExpr ')'
;

enum RoamStreamSetOperator:
	FILTER='filter'
;

enum RoamStreamArithmeticOperator:
	SUM = 'sum' |
	count = 'count'
;

enum RoamStreamBooleanOperator:
	EXISTS = 'exists' |
	NOTEXISTS = 'notExists'
;


RoamArithmeticExpr returns RoamArithmeticExpr:
	RoamUnaryArithmeticExpr({RoamBinaryArithmeticExpr.left = current} operator=RoamArithmeticBinaryOperator right=RoamUnaryArithmeticExpr)*
;

RoamUnaryArithmeticExpr returns RoamArithmeticExpr: {RoamUnaryArithmeticExpr}
	(negative?='-') (operator=RoamArithmeticUnaryOperator'(' | '(') operand=RoamArithmeticExpr')' | RoamExpressionOperand
;

RoamExpressionOperand:
	RoamStream | RoamAttributeExpr | ArithmeticLiteral  //| RoamStream //|RoamStreamAttributeExpression //| StringLiteral | EnumLiteral |
;

ArithmeticLiteral:
	value = (DoubleLiteral | IntegerLiteral)
;

enum RoamArithmeticBinaryOperator:
	PLUS='+' 	|
	MINUS='-' 	|
	MULT='*'|
 	DIV='/' 	|
 	POW='^'	
;

enum RoamArithmeticUnaryOperator:
	NONE='$NO_OP'     |
	ABS='abs'	|
	SQRT='sqrt' |
	sin='sin'	|
	cos='cos'	
;

RoamBoolExpr returns RoamBoolExpr:
	RoamUnaryBoolExpr({RoamBinaryBoolExpr.left = current} operator=RoamBoolBinaryOperator right=RoamUnaryBoolExpr)*
;

RoamUnaryBoolExpr returns RoamBoolExpr: {RoamUnaryBoolExpr}
	(operator=RoamBoolUnaryOperator '(' | '(') operand=RoamBoolExpr')' | RoamRelExpr | RoamAttributeExpr | BooleanLiteral
;

BooleanLiteral:
	value = BOOLEAN
;

enum RoamBoolBinaryOperator:
	AND='&' 	|
	OR='|'	
;

enum RoamBoolUnaryOperator:
	NONE='$NO_OP'     |
	NOT='!'	
;

RoamRelExpr:
	left=RoamArithmeticExpr operator=RoamRelOperator right=RoamArithmeticExpr
;

enum RoamRelOperator:
  GREATER='>' |
  GREATER_OR_EQUAL='>=' |
  EQUAL='==' |
  UNEQUAL='!=' |
  SMALLER_OR_EQUAL='<=' |
  SMALLER='<'
;

StringLiteral:
	value = STRING
;

DoubleLiteral:
	Double
;

IntegerLiteral:
	Integer
;

EnumLiteral:
	'enum::' value = [ecore::EEnumLiteral]
;

Double returns ecore::EDouble:
	('-')?INT '.' INT
;

Integer returns ecore::EInt:
	(('-')? INT)
;

@Override
terminal INT returns ecore::EInt:
	'0'..'9' ('0'..'9')*
;

@Override
terminal STRING returns ecore::EString:
	'"' ( '""' | !('"') )* '"'
;

BOOLEAN returns ecore::EBoolean:
	TRUE | FALSE
;

terminal TRUE returns ecore::EBoolean:
	'true'
;

terminal FALSE returns ecore::EBoolean:
	'false'
;